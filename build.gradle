plugins {
	id 'com.github.kt3k.coveralls' version '2.4.0'
	id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'jacoco'

group                        = 'net.querz.nbt'
archivesBaseName             = 'nbt'
version                      = '6.1'
compileJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'junit:junit:4.12'
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.implementation
	destinationDir = file("./doc/")
	options.windowTitle 'NBT'
	options.encoding 'UTF-8'
	options.linkSource true
	options.links 'https://docs.oracle.com/javase/8/docs/api/'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            version = project.version
            artifactId = archivesBaseName
            groupId = group
        }
    }
    repositories {
        if (System.getenv('MAVEN_USERNAME') != null && System.getenv('MAVEN_PASSWORD') != null) {
            maven {
                name 'release'
                url = 'https://maven.nova-committee.cn/releases'

                credentials {
                    username System.getenv('MAVEN_USERNAME')
                    password System.getenv('MAVEN_PASSWORD')
                }
            }
        }
    }
}

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}
